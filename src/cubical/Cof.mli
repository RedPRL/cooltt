(** {1 Types } *)

(** Multiple types in [cooltt] will need to {i include} the langauge of cofibrations, relative to a particular interval algebra ['r]. Therefore, we define a family polynomial endofunctors [('r, -) cof_f] indexed in an interpretation of the interval algebra ['r].
 *)
type ('r, 'a) cof_f =
  | Eq of 'r * 'r
  | Join of 'a list
  | Meet of 'a list

(** For each interval algebra ['r], we define the {i free monad} [('r, -) cof] on the polynomial endofunctor [('r, -) cof_f]: each [('r, 'v) cof] is the language of cofibrations over an interval algebra ['r], with indeterminates drawn from ['v]. *)
type ('r, 'v) cof =
  | Cof of ('r, ('r, 'v) cof) cof_f
  | Var of 'v

(** {1 Smart constructors} *)

(** The cofibration corresponding to a variable [Ï† : ð”½]. *)
val var : 'v -> ('a, 'v) cof

(** Given dimensions [r, r' : ð•€], the cofibration [r=r']. *)
val eq : 'a -> 'a -> ('a, 'v) cof

(** Given a list [Ï†0,...,Ï†n : ð”½] of cofibrations, the disjunction [Ï†0 âˆ¨ ... âˆ¨ Ï†n]. *)
val join : ('a, 'v) cof list -> ('a, 'v) cof

(** Given a list [Ï†0,...,Ï†n : ð”½] of cofibrations, the conjunction [Ï†0 âˆ§ ... âˆ§ Ï†n]. *)
val meet : ('a, 'v) cof list -> ('a, 'v) cof

(** The false cofibration, equivalent to [join []]. *)
val bot : ('a, 'v) cof

(** The true cofibration, equivalent to [meet []]. *)
val top : ('a, 'v) cof

(** The boundary [âˆ‚r] of a dimension [r : ð•€] is the disjunction [r=0 âˆ¨ r=1] *)
val boundary : Dim.dim -> (Dim.dim, 'v) cof

