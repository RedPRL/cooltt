def symm : {
  (A : univ) (x : A) (y : A) (p : Id A x y)
  -> Id A y x
} = {
  \A x y p =>
  elim p @ {u v _ => Id A v u} [
  | refl => refl
  ]
}

def trans : {
  (A : univ) (x : A) (y : A) (z : A)
  (p : Id A x y) (q : Id A y z)
  -> Id A x z
} = {
  \A x y z p =>
  elim p @ {u v _ => (q : Id A v z) -> Id A u z} [
  | refl => \q => q
  ]
}

def trans-assoc : {
  (A : univ) (s : A) (t : A) (u : A) (v : A)
  (p : Id A s t) (q : Id A t u) (r : Id A u v)
  -> Id
      {Id A s v}
      {trans A s u v {trans A s t u p q} r}
      {trans A s t v p {trans A t u v q r}}
} = {
  \A s t u v p =>
  elim p @ {s t p =>
    (q : Id A t u) (r : Id A u v)
    -> Id
        {Id A s v}
        {trans A s u v {trans A s t u p q} r}
        {trans A s t v p {trans A t u v q r}}
  } [
  | refl => \q r => refl
  ]
}

def trans-left-unit : {
  (A : univ) (x : A) (y : A) (p : Id A x y)
  -> Id {Id A x y} {trans A x x y refl p} p
} = {
  \A x y p => refl
}

def trans-right-unit : {
  (A : univ) (x : A) (y : A) (p : Id A x y)
  -> Id {Id A x y} {trans A x y y p refl} p
} = {
  \A x y p =>
  elim p @ {x y p => Id {Id A x y} {trans A x y y p refl} p} [
  | refl => refl
  ]
}

def trans-symm-refl : {
  (A : univ) (x : A) (y : A) (p : Id A x y)
  -> Id {Id A x x} {trans A x y x p {symm A x y p}} refl
} = {
  \A x y p =>
  elim p @ {x y p =>
    Id {Id A x x} {trans A x y x p {symm A x y p}} refl
  } [
  | refl => refl
  ]
}

def cong : {
  (A : univ) (B : univ) (f : A -> B)
  (x : A) (y : A) (p : Id A x y)
  -> Id B {f x} {f y}
} = {
  \A B f x y p =>
  elim p @ {u v _ => Id B {f u} {f v}} [
  | refl => refl
  ]
}

def + : nat -> nat -> nat = {
  \elim [
  | zero => \n => n
  | suc {_ => ih} => \n => suc {ih n} 
  ]
}

def +-right-unit : (x : nat) -> Id nat {+ x 0} x = {
  \elim [
  | zero => refl
  | suc {y => ih} =>
    cong nat nat {\z => suc z} {+ y 0} y ih
  ]
}

def +-right-unit/cubical : (x : nat) (i : ð•€) -> sub nat {i=0 âˆ¨ i=1} [i=0 => {+ x 0} | i=1 => x] = {
  \elim [
  | zero => \i => 0
  | suc {x => ih} => \i => suc {ih i}
  ]
}

def +-left-unit : (x : nat) -> Id nat {+ 0 x} x = {
  \_ =>
  refl
}

def +-suc-r : (x : nat) (y : nat) -> Id nat {+ x {suc y}} {suc {+ x y}} = {
  \elim [
  | zero =>
    \_ => refl
  | suc {x => ih} =>
    \y =>
    cong nat nat {\w => suc w} {+ x {suc y}} {suc {+ x y}} {ih y}
  ]
}

def +-comm : (x : nat) (y : nat) -> Id nat {+ y x} {+ x y} = {
  \elim [
  | zero => +-right-unit
  | suc {y => ih} =>
    \z =>
    trans nat {+ z {suc y}} {suc {+ z y}} {suc {+ y z}}
      {+-suc-r z y}
      {cong nat nat {\w => suc w} {+ z y} {+ y z} {ih z}}
  ]
}

def +-assoc : {
  (x : nat) (y : nat) (z : nat) ->
  Id nat {+ {+ x y} z} {+ x {+ y z}}
} = {
  \elim [
  | zero => \y z => refl
  | suc {x => ih} =>
    \y z =>
    cong nat nat {\w => suc w} {+ {+ x y} z} {+ x {+ y z}} {ih y z}
  ]
}
